/* Internal FLASH linker command file for MPC5674F                          */
/*                                                                          */
/*   +-----------+------------+------------+                                */
/*   |  Device   | MPC5674F   | MPC5673F   |                                */
/*   +-----------+------------+------------+                                */
/*   |SRAM/Flash | 256KB/     | 192KB/     |                                */
/*   |           | 4MB        | 3MB        |                                */
/*   +-----------+------------+------------+                                */
/*                                                                          */
/* Intended to be used for the stationary example project.                  */
/*                                                                          */
/* VERSION: 1.2                                                             */

MEMORY
{
    /* FLASH: 0x00000000 - 0x0003FFFFF */
    /* Fixed location required for RCHW and program entry point*/
    resetvector:           org = 0x00000000,   len = 0x00000008

    /* Contains initializations from __ppc_eabi_init.c,
     MPC56xx_HWInit.c, MPC56xx_init_*.c and the entry point (__startup).
     Should be 4K since the hardware automatically creates a default TLB entry
     from the start of the entry point specified in RCHW.
     */
    init:                   org = 0x00000010,   len = 0x00000FF0 /* ~4K */

	/* MPC5674F_Porting_Modif : Add the memory areas of trampoline OS */ 
	//critical_input :       org = 0x00010000,   len = 0x10
    //machine_check :        org = 0x00010010,   len = 0x10
    //data_storage :         org = 0x00010020,   len = 0x10
    //instruction_storage :  org = 0x00010030,   len = 0x10
    //external_interrupt :   org = 0x00010040,   len = 0x10
    //alignment :            org = 0x00010050,   len = 0x10
    //program :              org = 0x00010060,   len = 0x10
    //fp_unavailable :       org = 0x00010070,   len = 0x10
    //system_call:           org = 0x00010080,   len = 0x10
    
	/* ~63K gap required to align the exception handlers.*/

    /* Contains interrupt branch tables for both core and INTC module
     and the ISR handlers code. Note, the gap is required since the vector
     base address field of core IVPR is defined within the range [0:15], the
     INTC branch tables must be loaded at an address aligned to 64K boundary.
     */
    exception_handlers: org = 0x00010000,   len = 0x0020000 /* 128K */

    /* Space allocated for user code and device initialization.
    ROM Image address should be set with the start address of this
    segment in order to instruct the runtime to initialize the
    static variables. All the section above are ignored for this action. 
    Please see -romaddr linker option.*/
    internal_flash:        org = 0x00030000,   len = 0x003D0000 /* 3904K */

    /* SRAM: 0x40000000 - 0x4003FFFF */
    internal_ram:       org = 0x40000000,   len = 0x00030000 /* 192KB */
    heap  :             org = 0x40030000,   len = 0x00008000 /* 32KB Heap start location */
    stack :             org = 0x40038000,   len = 0x00008000 /* 32KB Start location for Stack */
}

/* This will ensure the rchw and reset vector are not stripped by the linker */
FORCEACTIVE { "bam_rchw" "bam_resetvector"}

SECTIONS
{
    .__bam_bootarea LOAD (ADDR(resetvector)): {} > resetvector

    /* Section used for initialization code: __ppc_eabi_init.c,
     MPC56xx_HWInit.c, MPC56xx_init_*.c and the entry point (__startup).
     */
    GROUP  : {
        .init LOAD (ADDR(init)) : {}
        .init_vle (VLECODE) LOAD (_e_init) : {
            *(.init)
            *(.init_vle)
        } 
    } > init
	
    /* MPC5674F_Porting_Modif : Copy the trampoline used sections to the corresponding memory areas */ 
	//.critical_input      LOAD (0x00010000) : { *(.exCriticalInput) }            > critical_input
	//.machine_check       LOAD (0x00010010) : { *(.exMachineCheck) }             > machine_check
	//.data_storage        LOAD (0x00010020) : { *(.exDataStorage) }              > data_storage
	//.instruction_storage LOAD (0x00010030) : { *(.exInstructionStorage) }       > instruction_storage
	//.external_interrupt  LOAD (0x00010040) : { *(.EI_vector) }                  > external_interrupt
	//.alignment           LOAD (0x00010050) : { *(.exAlignment) }                > alignment
	//.program             LOAD (0x00010060) : { *(.exProgram) }                  > program
	//.fp_unavailable      LOAD (0x00010070) : { *(.exFloatingPointUnavailable) } > fp_unavailable 
	//.system_call         LOAD (0x00010080) : { *(.SC_vector) }                  > system_call

   /* Note: _e_ prefix enables load after END of that specified section */
   GROUP : {
        /* Special section for INTC branch table required in hardware mode.
        Place the .intc_hw_branch_table section first in order to used both core and INTC
        tables. The intc_hw_branch_table should contain entries aligned to 16 bytes.
        */
       .intc_hw_branch_table (VLECODE) LOAD (ADDR(exception_handlers)) ALIGN (0x10) : {}
 
        /* Because the core IVORx are settable the IVOR branch table can be placed
        after the INTC HW table.*/
        /* MPC5674F_Porting_Modif : Commented, the handlers corresponding to the exceptions vectors will be managed by the OS */
        //.ivor_branch_table (VLECODE) LOAD (_e_intc_hw_branch_table) ALIGN (0x10) : {}
        
        .exCriticalInput (VLECODE) LOAD (_e_intc_hw_branch_table) ALIGN (0x10) : {}
        .exMachineCheck (VLECODE) LOAD (_e_exCriticalInput) ALIGN (0x10) : {}
        .exDataStorage (VLECODE) LOAD (_e_exMachineCheck) ALIGN (0x10) : {}
        .exInstructionStorage (VLECODE) LOAD (_e_exDataStorage) ALIGN (0x10) : {}
        .EI_vector (VLECODE) LOAD (_e_exInstructionStorage) ALIGN (0x10) : {}
        .exAlignment (VLECODE) LOAD (_e_EI_vector) ALIGN (0x10) : {}
        .exProgram (VLECODE) LOAD (_e_exAlignment) ALIGN (0x10) : {}
        .exFloatingPointUnavailable (VLECODE) LOAD (_e_exProgram) ALIGN (0x10) : {}
        .SC_vector (VLECODE) LOAD (_e_exFloatingPointUnavailable) ALIGN (0x10) : {}
        
        .exFixedIntervalTimer (VLECODE) LOAD (_e_SC_vector) ALIGN (0x10) : {} 
        .exWatchdogTimer (VLECODE) LOAD (_e_exFixedIntervalTimer) ALIGN (0x10) : {}
        .exDataTLBError (VLECODE) LOAD (_e_exWatchdogTimer) ALIGN (0x10) : {}
        .exInstructionTLBError (VLECODE) LOAD (_e_exDataTLBError) ALIGN (0x10) : {}
        
        /* ISR handlers code. */
        .__exception_handlers (VLECODE) LOAD (_e_exInstructionTLBError) ALIGN (0x10) : {}
   } > exception_handlers

	/* F_TODO : Set the osApiCode in the .text section */
    GROUP  : {
		.osCode (VLECODE) ALIGN(32) : {
			__PROGCONST_SECTION_START = .;
			*(.EI_handler)
			*(.SC_handler)
			*(.osCode)
			*(.appCommonCode)
			*(.osApiCode)
		}
        .text : { 
        	*(.text)
        }
        .text_vle (VLECODE) : {
   		    *(.text)
            *(.text_vle)   
        }
       
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
			*(.osConst)
      		*(.osApiConst)    
/* Sections for application const */

        }
       
       .ctors : {}
       .dtors : {}
       extab : {}
       extabindex : {}
	   
    __PROGCONST_SECTION_STOP = ALIGN(32) - 1;
	   
    } > internal_flash

    GROUP : {
       /* This section is used in INTC SW mode to store the interrupt handlers array.
        Although INTC_IACKR.VTBA is 21-bit wide it should be aligned to 4K since if
        INTC_MCR.VTES == 0 only 20 bits are actually used.*/
       .__uninitialized_intc_handlertable ALIGN(0x1000) : {}
       .data   : {}
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .bss    : {}
	   
	   .osVar (DATA) : { *(.osVar) }
       .osVarNoInit (BSS) : { *(.osVarNoInit) }
  
 /* os application vars */
	   
    } > internal_ram
}

/* Freescale CodeWarrior compiler address designations */

_stack_addr = ADDR(stack)+SIZEOF(stack);
_stack_end  = ADDR(stack);
_heap_addr  = ADDR(heap);
_heap_end   = ADDR(heap)+SIZEOF(heap);

/* If INTC HW mode is used it represents the vector base address to set
IVPR and the location of intc_hw_branch_table section. The EXCEPTION_HANDLERS
will point to the IVOR branch table.
*/
__IVPR_VALUE = ADDR(exception_handlers);

/* IVOR branch table location. Used in Exceptions.c */
EXCEPTION_HANDLERS = ADDR(.exCriticalInput);

/* L2 SRAM Location (used for L2 SRAM initialization) */
L2SRAM_LOCATION = ADDR(internal_ram);

/* How many writes with stmw, 128 bytes each, are needed to cover
   the whole L2SRAM (used for L2 SRAM initialization) */
L2SRAM_CNT = 0x40000 / 128;

/* External SRAM module */
START_EXTERNAL_RAM = 0x20000000;
